apply plugin: 'maven-publish'

// 强制 Java/JavaDoc 等的编码为 UTF-8
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc).configureEach {
    options.encoding = "UTF-8"
}

def javadocJar = task("javadocJarLocal", type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

def sourcesJar = task("sourcesJarLocal", type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.java.srcDirs
}

publishing {
    repositories {
        maven { url uri("../repo") }
    }

    publications {
        Component(MavenPublication) {
            from components.java
            groupId = publishedGroupId
            artifactId = artifact
            version = TRACEFIX_VERSION_DEBUG

            artifact sourcesJarLocal
            artifact javadocJarLocal
        }
    }
}


task buildAndPublishToLocalMaven(type: Copy, dependsOn: ['build', 'publishToMavenLocal']) {
    // save artifacts files to artifacts folder
    from configurations.archives.allArtifacts.files
    into "${rootProject.buildDir}/outputs/artifacts/"
    rename { String fileName ->
        fileName.replace("release.aar", "${version}.aar")
    }

    doLast {
        println "* published to maven local: ${project.group}:${project.name}:${project.version}"
    }
}